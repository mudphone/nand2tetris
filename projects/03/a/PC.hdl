// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16(in=o0, out=i0);
    Mux16(a=o0, b=i0, sel=inc, out=inco);

    Mux16(a=inco, b=in, sel=load, out=lo);
    Mux16(a=lo, b=false, sel=reset, out=ro);

    Or(a=inc, b=load, out=or0);
    Or(a=or0, b=reset, out=or1);
    Register(in=ro, load=or1, out=out, out=o0);
}
