// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:

    // inst[5] == write ALU out to A
    Mux16(a[0..14]=instruction[0..14], b=aluo, sel=instruction[15], out=toareg);
    Not(in=instruction[15], out=ninst15);
    And(a=instruction[5], b=instruction[15], out=loadaddresscmd);
    Or(a=ninst15, b=loadaddresscmd, out=loadareg);
    ARegister(in=toareg, load=loadareg, out=areg, out[0..14]=addressM[0..14]);

    // inst[4] == write ALU out to D
    And(a=instruction[4], b=instruction[15], out=loaddatacmd);
    DRegister(in=aluo, load=loaddatacmd, out=dreg);

    // writeM
    And(a=instruction[3], b=instruction[15], out=writeM);
    
    Mux16(a=areg, b=inM, sel=instruction[12], out=y);
    ALU(x=dreg, y=y, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=aluo, out=outM, zr=zr, ng=ng);

    // j:inst[1] => out=0
    And(a=instruction[1], b=zr, out=eq0);
    // j:inst[2] => out<0
    And(a=instruction[2], b=ng, out=lt0);
    // j:inst[0] => out>0
    Not(in=zr, out=nzr);
    Not(in=ng, out=nng);
    And(a=nzr, b=nng, out=pos);
    And(a=instruction[0], b=pos, out=gt0);
    // JMP
    And(a=instruction[0], b=instruction[1], out=jmpa);
    And(a=jmpa, b=instruction[2], out=jmp);
    // is jump?
    Or(a=eq0, b=lt0, out=jmp0);
    Or(a=jmp0, b=gt0, out=jmp1);
    Or(a=jmp1, b=jmp, out=jmp2);
    And(a=jmp2, b=instruction[15], out=jump);
    Not(in=jump, out=inc);
    PC(in=areg, load=jump, inc=inc, reset=reset, out[0..14]=pc);
}
